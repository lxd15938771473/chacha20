import "poly64.cry";
llvm <- llvm_load_module "poly.bc";


let alloc_init typ value = do {
    ptr <- crucible_alloc typ;
    crucible_points_to ptr (crucible_term value);
    return ptr;
};

let ptr_to_fresh name typ = do {
    x <- crucible_fresh_var name typ;
    xptr <- alloc_init typ x;
    return (x, xptr);
};

let U8TOU64_setup = do {
    (arr,arrp) <- ptr_to_fresh "array" (llvm_array 8 (llvm_int 8));  //第一个是cryptol参数第二个是c参数
    llvm_execute_func [arrp];
    let arr' = {{ U8TOU64 arr }};
    llvm_return (llvm_term {{arr'}});
};

// llvm_verify llvm "U8TOU32" [] false U8TOU32_setup (w4_unint_z3 []);
u8tou64 <- llvm_verify llvm "U8TOU64" [] false U8TOU64_setup yices;


let U64TO8_setup = do {
    (arr,arrp) <- ptr_to_fresh "array" (llvm_array 8 (llvm_int 8));  //第一个是cryptol参数第二个是c参数
    v <- llvm_fresh_var "v" (llvm_int 64);
    llvm_execute_func [arrp, llvm_term v];           //非指针要这样声明
    let v' = {{ U64TO8 v }};
    llvm_points_to arrp (llvm_term v');
};

u64to8 <- llvm_verify llvm "U64TO8" [] false U64TO8_setup yices;


let poly1305_init_setup = do {
    (key,keyp) <- ptr_to_fresh "key" (llvm_array 16 (llvm_int 8));  //第一个是cryptol参数第二个是c参数
    poly1305_internal <- llvm_alloc (llvm_struct "struct.poly1305_internal");
    poly1305_internal_h <- llvm_fresh_var "h" (llvm_array 3 (llvm_int 64));
    poly1305_internal_r <- llvm_fresh_var "r" (llvm_array 2 (llvm_int 64));
    llvm_points_to (llvm_field poly1305_internal "h") (llvm_term poly1305_internal_h);
    llvm_points_to (llvm_field poly1305_internal "r") (llvm_term poly1305_internal_r);

    llvm_execute_func [poly1305_internal, keyp];
    let poly1305_internal'' = {{ poly1305_init key}};
    llvm_points_to (llvm_field poly1305_internal "h") (llvm_term {{ poly1305_internal''.h }});
    llvm_points_to (llvm_field poly1305_internal "r") (llvm_term {{ poly1305_internal''.r }});
};

llvm_verify llvm "poly1305_init" [u8tou64] false poly1305_init_setup (w4_unint_z3 []);


let poly1305_blocks_setup n = do {
    poly1305_internal <- llvm_alloc (llvm_struct "struct.poly1305_internal");
    poly1305_internal_h <- llvm_fresh_var "h" (llvm_array 3 (llvm_int 64));
    poly1305_internal_r <- llvm_fresh_var "r" (llvm_array 2 (llvm_int 64));
    llvm_points_to (llvm_field poly1305_internal "h") (llvm_term poly1305_internal_h);
    llvm_points_to (llvm_field poly1305_internal "r") (llvm_term poly1305_internal_r);
    padbit <- llvm_fresh_var "padbit" (llvm_int 32);
    (inp, inpp) <- ptr_to_fresh "inp" (llvm_array n (llvm_int 8));
    llvm_execute_func [poly1305_internal, inpp, llvm_term {{`n:[64]}}, llvm_term padbit];          //要使用`n
    let poly1305_internal' = {{ {h = poly1305_internal_h, r = poly1305_internal_r} }};
    let poly1305_internal'' = {{ poly1305_blocks poly1305_internal' inp padbit }};
    llvm_points_to (llvm_field poly1305_internal "h") (llvm_term {{ poly1305_internal''.h }});
    llvm_points_to (llvm_field poly1305_internal "r") (llvm_term {{ poly1305_internal''.r }});
    // let inp' = {{ inp }};
    // llvm_points_to inpp (llvm_term inp');
};

llvm_verify llvm "poly1305_blocks" [] true (poly1305_blocks_setup 128) yices;



let poly1305_emit_setup = do {
    (mac, macp) <- ptr_to_fresh "mac" (llvm_array 16 (llvm_int 8));  //第一个是cryptol参数第二个是c参数
    (nonce, noncep) <- ptr_to_fresh "nonce" (llvm_array 4 (llvm_int 32)); 
    poly1305_internal <- llvm_alloc (llvm_struct "struct.poly1305_internal");
    poly1305_internal_h <- llvm_fresh_var "h" (llvm_array 3 (llvm_int 64));
    poly1305_internal_r <- llvm_fresh_var "r" (llvm_array 2 (llvm_int 64));
    llvm_points_to (llvm_field poly1305_internal "h") (llvm_term poly1305_internal_h);
    llvm_points_to (llvm_field poly1305_internal "r") (llvm_term poly1305_internal_r);

    llvm_execute_func [poly1305_internal, macp, noncep];
    let poly1305_internal' = {{ {h = poly1305_internal_h, r = poly1305_internal_r} }};
    let mac' = {{ poly1305_emit poly1305_internal' nonce }};
    llvm_points_to macp (llvm_term mac');
};

llvm_verify llvm "poly1305_emit" [] false poly1305_emit_setup yices;